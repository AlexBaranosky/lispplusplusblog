---
layout: post
title: "The Evolution of a Macro"
date: 2013-07-28T02:59:00-07:00
comments: false
categories:
 - clojure
 - testing
 - macro
---

<div class='post'>
<div dir="ltr" style="text-align: left;" trbidi="on">At Runa, where I work, I work on a very large Clojure application which we service online merchants with in order to deliver real-time promotions to the browser. &nbsp;The runtime promotion determination core of the application is quite intricate and has both stateful and immutable parts. &nbsp;If not tested properly, you can make the mistake of letting any setup mutable state effect tests other than the intended test case.<br /><br />I thought it'd be fun to walk you through how I developed a macro to handle this type of situation at work. &nbsp;I think it is a pretty interesting example of how macros can evolve and what considerations might go into how you architect a meaty macro.<br /><br /><i>NOTE: The code has been simplified from the production code to better emphasize the important points.</i><br /><br /><h3 style="text-align: left;">First Stab At a Stateful Test Macro</h3><br />Our first stab at this problem solves the basic initial version of the problem. &nbsp;Often this would be all you need to get the job done. &nbsp;This approach enables us to evaluate our test assertion from within a stateful scope, and we use the finally block of the try-catch to ensure that the rules are always set back to their original state when the test is done.<br /><br /><pre class="brush: clj; gutter: false;">(defmacro with-rules [rules &amp; body]<br />  `(let [old-rules# @rules]<br />     (try<br />       (reset! rules (evaluate-rules ~rules))<br />       ~@body<br />       (finally<br />         (reset! rules old-rules#)))))<br /><br />(deftest test-priority-level<br /> (with-rules (def-rule priority-level 5)<br />  (is (= true (priority-level 5)))))<br /></pre><br />Then if we have some other sort of state, for example machine learning models, we can write another `with-models` macro and combine the macros in a test.<br /><br /><pre class="brush: clj; gutter: false;">(defmacro with-models [models &amp; body]<br /> `(let [old-models# @models]<br />  (try<br />   (reset! models ~models)<br />   ~@body<br />   (finally<br />    (reset! models old-models#)))))<br /><br />(deftest test-model-and-level<br /> (with-rules (def-rule priority-level 5)<br />  (with-models {:a [1 2 3 4 5 6]<br />                :b [7 8 9 10 11 12]} ;; insert fancy math stuff here ;)<br />   (is (= true (found-promotion? (legal-customer-session)))))))<br /></pre><br /><div>Before I make a serious change to the approach we use for this kind of macro, let's remove the bloat from these macros and make them lean mean macros with beefier functions to go with them.</div><div><br /></div><pre class="brush: clj; gutter: false;">(defn with-rules* [rules body-fn]<br /> (let [old-rules @rules]<br />  (try<br />   (reset! rules (evaluate-rules rules))<br />   (body-fn)<br />   (finally<br />    (reset! rules old-rules)))))<br /><br />(defmacro with-rules [rules &amp; body]<br /> `(with-rules* ~rules (fn [] ~@body)))<br /><br />(defn with-models* [models body-fn]<br /> (let [old-models @models]<br />  (try<br />   (reset! models models)<br />   (body-fn)<br />   (finally<br />    (reset! models old-models)))))<br /><br />(defmacro with-models [models &amp; body]<br /> `(with-models* ~models (fn [] ~@body)))<br /></pre><br /><div><br />Why? Because macros are harder to read, and not composable.</div><div><br /></div><div>This way of approaching the stateful setup and teardown in our tests gets the job done, but has a couple issues. &nbsp;As soon as we have another source of state we need another with-x macro, and when we combine these together we end up over time with code that looks like this.</div><div><br /></div><pre class="brush: clj; gutter: false;">(deftest test-motherload<br /> (with-x X<br />  (with-y Y<br />   (with-z Z<br />    (with-a A<br />     (with-b B<br />       (is (= 42 (evaluate)))))))))<br /></pre><br />What I really want here is one macro not 5! &nbsp;I'd like something I can easily extend as new forms of state come up.<br /><br /><h3 style="text-align: left;">Composing Functions For A More Extensible Approach</h3><br />What if we could write test-motherload like this:<br /><br /><pre class="brush: clj; gutter: false;">(deftest test-motherload<br /> (with-runtime-state {:x X :y Y :z Z :a A :b B}<br />  (is (= 42 (evaluate)))))<br /></pre><br />And ideally if a C type of state comes up down the line, it'll be straightforward to extend this macro to accomodate it without pulling your hair out dealing with #'s, ~'s, @'s, ''s and `'s.<br /><br />With a minor change to with-rules* and with-models* we can enable them to be used to compose 0-arity functions into other 0-arity functions.<br /><br /><pre class="brush: clj; gutter: false;">(defn rules-setup-and-teardown [rules body-fn]<br /> (fn []<br />  (let [old-rules @rules]<br />   (try<br />    (reset! rules (evaluate-rules rules))<br />    (body-fn)<br />    (finally<br />     (reset! rules old-rules))))))<br /><br />(defn models-setup-and-teardown [models body-fn]<br /> (fn []<br />  (let [old-models @models]<br />   (try<br />    (reset! models models)<br />    (body-fn)<br />    (finally<br />      (reset! models old-models)))))<br /> </pre><br /><div>We can take the testcase evaluation function and transform it into a function that when called will wrap the evaluate-test-case function in setup and teardown of the rules state.<br /><br /><pre class="brush: clj; gutter: false;"><br />(rules-setup-and-teardown '(def-rule priority-level 5) (fn [] (evalute-test-case)))<br /><br /></pre><br />And we can chain these function compositions together, to incorporate more varieties of setup and teardown as necessary. &nbsp;Note we don't need no fancy macros for this (yet).<br /><br /><pre class="brush: clj; gutter: false;">(deftest test-model-and-level<br /> (let [testcase-fn (fn []<br />                     (is (= true (found-promotion? (legal-customer-session)))))<br />       wrapped-testcase-fn <br />         (->> testcase-fn<br />              (rules-setup-and-teardown '(def-rule priority-level 5))<br />              (models-setup-and-teardown {:a [1 2 3 4 5 6]<br />                                          :b [7 8 9 10 11 12]}))]<br />   (wrapped-testcase-fn)))<br /></pre><br />Remember what we're aiming for is something more like this:<br /><br /><pre class="brush: clj; gutter: false;">(deftest test-motherload<br /> (with-runtime-state {:x X :y Y :z Z :a A :b B}<br />   (is (= 42 (evaluate)))))<br /></pre><br />This is going to require us to convert the state map, {:x X :y Y :z Z :a A :b B}, into the appropriate setup-and-teardown wrapper functions, and do the wrapping of the body of the macro as well.<br /><br />The first definition option->setup-and-teardown-fn, maps keys from the state map to its corresponding setup-and-teardown function.<br /><br /><pre class="brush: clj; gutter: false;">(def ^:private option->setup-and-teardown-fn<br /> "Add new setup and teardown wrappers here, and the `with-runtime-state`<br />  macro automatically gets a new functionality"<br /> {:rules #'rules-setup-and-teardown<br />  :models #'models-setup-and-teardown})<br /></pre><div><br /></div><div>Finally the macro that makes it all fit together. &nbsp;If you look closely, it macro expands into something very similar to the composed function solution above.</div><div><br /></div><pre class="brush: clj; gutter: false;">(defmacro with-runtime-state [options &amp; body]<br /> (let [setup-and-teardowns<br />    ;; 1. map each key of the state-map to its corresponding setup-and-teardown fn<br />    (for [[k state-value] options]<br />     `((fn [f#]<br />       (let [setup-and-teardown# (get ~option->setup-and-teardown-fn ~k)]<br />        (setup-and-teardown# ~state-value f#)))))]<br />  ;; 2. thread the testcase body function through each wrapper fn, and evaluate it<br />  `((-> (fn [] ~@body)<br />     ~@setup-and-teardowns))))<br /></pre><br />We can make the macro more user friendly with a couple of asserts on the args passed to it. &nbsp;These kinds of assertions are especially nice in macros, because they help make the macro's unique syntax easier to use for your teammates without them pulling their hair out.<br /><br />Also, we can be improve it one step further. &nbsp;We'll use clojure.core/partial to create the 1-arity setup-and-teardown functions, rather than wrapping each of them within the for expression.<br /><br /><pre class="brush: clj; gutter: false;">(def ^:private runtime-test-option-set (set (keys option->setup-and-teardown-fn)))<br /><br />(defmacro with-runtime-state [options &amp; body]<br /> (assert (map? options)<br />     "Options should be a map.")<br /> (assert (every? runtime-test-option-set (keys options))<br />     "Options should have :rules and/or :feature-bits keys")<br /> (let [setup-and-teardowns<br />    (for [[k state-value] options]<br />     `((partial (get ~option->setup-and-teardown-fn ~k) ~state-value)))]<br />  `((-> (fn [] ~@body)<br />     ~@setup-and-teardowns))))<br /></pre><br /><h3 style=""text-align: left>Refactoring Addendum</h3><br />I thought a little more about this and realized that even this macro is doing too much.  We can actually extract the code that wraps a 0-arity function f in setup-and-teardown functions. In the process we end up with a macro with one line of code that does the heavy lifting and a few more lines of asserts.  <pre class="brush: clj; gutter: false;"><br />(defn wrap-in-setups-and-tear-downs [options f]<br />  (let [setup-and-teardown-fns (for [[k state-value] options]<br />                                 (partial (get option->setup-and-teardown-fn k) <br />                                          state-value))]<br />    (reduce (fn [f' setup-and-teardown-fn]<br />              (setup-and-teardown-fn f'))<br />            f<br />            setup-and-teardown-fns)))<br /><br />(defmacro with-runtime-state [options & body]<br />  (assert (map? options)<br />          "Options should be a map.")<br />  (assert (every? runtime-test-option-set (keys options))<br />          "Options should have :rules and/or :feature-bits keys")<br />  `((wrap-in-setups-and-tear-downs ~options (fn [] ~@body))))<br /></pre> This is a useful step because now we have another higher order function with which to build useful state-management tooling, and we've eliminated more of the potentially confusing macro code. <br /><br /><h3 style="text-align: left;">Takeaways</h3><br />So why go to all this trouble?<br /><br /><ul style="text-align: left;"><li>We've written a macro that is extensible.</li><li>We've minimized actual lines of macro code. Yay!</li><li>We get validation of the state-map keys for free, anytime we extend this with a new setup-and-teardown function.</li><li>Our tests have a clean, uniform, minimal DSL for expressing pre-requisite mutable state.</li></ul><br />How did we do it?<br /><br /><ul style="text-align: left;"><li>We took an essentially functional compositional approach.&nbsp;</li><li>We wrapped it in a layer of syntactic sugar.</li></ul><br />Hope you enjoyed this little code walk through.<br /><br />Comments or questions?</div></div></div>
<h2>Comments</h2>
<div class='comments'>
<div class='comment'>
<div class='author'>Petr</div>
<div class='content'>
One more reason that using dynamic scope sucks.<br /><br />BTW Have you thought of replacing set of *-setup-and-teardown with set of<br />{:setup (fn [] ....) :teardown (fn [] ....)} <br />and not repeating (try ... (finally ...)) every time?</div>
</div>
<div class='comment'>
<div class='author'>Alex Baranosky</div>
<div class='content'>
Thanks.  I have at least got it into a stable state that should no longer break in Chrome.</div>
</div>
<div class='comment'>
<div class='author'>regeya</div>
<div class='content'>
cannot find brush for: clj<br /><br />That&#39;s the dialog that keeps popping up every time I visit a page on this blog.</div>
</div>
<div class='comment'>
<div class='author'>Alex Baranosky</div>
<div class='content'>
Hi Alex, <br /><br />Thanks for the comment.  I actually thought I had added the appropriate tags last evening when I wrote it, but must have not saved my change.</div>
</div>
<div class='comment'>
<div class='author'>Alex Ott</div>
<div class='content'>
Hi <br /><br />thank you for posting this!<br />can you also add the clojure label to it, so it will be propagated into Planet Clojure?</div>
</div>
</div>
