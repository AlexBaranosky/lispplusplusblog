---
layout: post
title: "The Many Faces of Midje Fakes"
date: 2012-02-27T19:21:00-08:00
comments: false
categories:
 - midje
 - clojure
---

<div class='post'>
The <a href="https://github.com/marick/Midje">Midje</a> Clojure testing library enables top-down development, and the primary tool it has for enabling it is the fake.<br /><br />In Midje, you don't have mock objects like you do in object-oriented languages, instead you fake out functions. The simplest way you can fake out functions is by using the provided form within your fact.<br /><br /><h3>Fakes</h3><pre class="brush: clj; gutter: false;"><br />(unfinished doubler)<br /><br />(defn six-times [x]<br />  (* 3 (doubler x)))<br /><br />(fact<br />  (six-times ..n..) => 12<br />  (provided<br />    (doubler ..n..) => 4))<br /></pre> <br/>(Things that look like ..x.. are called Midje metaconstants.  For now just pretend they are constants, or skip to the end of this article for a link to the wiki.)<br /><br />In the above example we've managed to define six-times in terms of a function, doubler, that is not yet defined (it is unfinished).<br /><br />Presumably the next thing we'd do when designing a piece of software like this is to write the fact for doubler.<br /><pre class="brush: clj; gutter: false;"><br />(fact <br />  (doubler 2) => 4)<br /><br />(defn doubler [n] (* 2 n))<br /></pre><br/>Or perhaps write a more thorough test:<br /><pre class="brush: clj; gutter: false;"><br />(tabular<br />  (fact (doubler n) => result)<br /><br />  n    result<br />  -2   -4<br />  -1   -2<br />  0    0<br />  1    2<br />  2    4<br />  100  200  )<br /></pre><br/>(Tabular is a way to write one fact that runs against multiple input sets.)<br /><br /><h3>The Myriad Uses of Fakes</h3><br />Fakes have other uses besides simple stubbing.<br /><br />They can also be used to verify call counts. Imagine that you want to check that when some data is not valid there are no calls to send-email, but otherwise there will be 1 call sent to each recipient?<br /><br />We can write this kind of test using the :times n prerequisite option. (where n is some non-negative integer) <pre class="brush: clj; gutter: false;"><br />(fact "don't send emails when data is invalid"<br />  (email-job :alex :brian) => irrelevant<br />  (provided<br />    (valid-data?) => false<br />    (email anything) => nil :times 0 ))<br /><br />(fact "when data is valid send one email to each recipient"<br />  (email-job :alex :brian) => irrelevant<br />  (provided<br />    (valid-data?) => true<br />    (email :alex) => nil :times 1<br />    (email :brian) => nil :times 1 ))<br /><br />;; The implementation<br /><br />(unfinished valid-data? email)<br /><br />(defn email-job [& dudes]<br />  (when (valid-data?)<br />    (doseq [d dudes]<br />      (email d))))<br /></pre><br /><h3>Checkers</h3><br />Using checkers we can even expand on the idea of what can be checked by a provided.  In the above examples first we used the anything checker to check that the email function was never called with anything. Then we simply checked that the email function was called with :alex and :brian once each.  <br /><br />In this example we want to check that the query function is called with 10 things that look like query maps. To do that we first need to create our own checker that checks whether the parameter looks like a query. <br/><pre class="brush: clj; gutter: false;"><br />(defchecker a-query [actual]<br />  (and (:target-db actual) (:query-plan actual)))<br /></pre><br />Midje will say that any arg matches this checker if it has a non-false/non-nil :target-db and :query-plan.<br /><br />Let's confirm query is called 10 times with params that look like queries.<br /><pre class="brush: clj; gutter: false;"><br />(fact "always sends 10 queries"<br />  (execute-queries) => irrelevant<br />  (provided<br />    (query a-query) => irrelevant :times 10))<br /><br />;; The implementation<br /><br />(unfinished query)<br /><br />(defn execute-queries []<br />  (dotimes [_ 10]<br />    (query {:target-db :foo       ;; this will pass because it looks like a-query<br />            :query-plan :bar})))  ;; but (query {:another "map"}) would not pass<br /><br /><br /></pre><h3>The Fake Chain Gang</h3><br />If you've ever violated The Law of Demeter in an object-oriented language and then tried to use mock objects with the offending class, you may have experienced a chain of mocks.<br /><br />A chain of mocks is when you mock a method on a mock object that itself returns another mock object, that has a method on it mocked to *finally* return the value you want.<br /><br />when(mockA.call()).thenReturn(mockB);<br />when(mockB.call()).thenReturn(mockC);<br />when(mockC.call()).thenReturn(42);<br /><br />In Midje the naive approach might be to write something like:<br /><pre class="brush: clj; gutter: false;"><br />(fact<br />  (foo) => 42<br />  (provided <br />    (baz) => ..a.. ;; remember, just pretend they're constants<br />    (bar ..a..) => ..b..<br />    (qux ..b..) => 42))<br /><br />;; The implementation<br /><br />(unfinished baz bar qux)<br /><br />(defn foo [] <br />  (qux (bar (baz))))<br /></pre><br/>Midje supplies a feature called Folded Prerequisites that enable a nice condensed syntax for this, though it is probably a code smell if you are doing this too often.<br /><pre class="brush: clj; gutter: false;"><br />(fact<br />  (foo) => 42<br />  (provided<br />    (qux (bar (baz))) => 42))<br /></pre><br /><h3>Fakes That Cover Multiple Facts</h3><br />There are other ways to make fakes in Midje as well, by using the background macro and the against-background macros. These give you the ability to write fakes that cover a group of fakes.  I think I'll talk about them in another blog though time permitting.<br /><br /><h3>Wiki Links</h3><a href="https://github.com/marick/Midje/wiki/Metaconstants">Metaconstants</a><br /><a href="https://github.com/marick/Midje/wiki/Checkers-within-prerequisites">Using Checkers in Fakes</a><br /><a href="https://github.com/marick/Midje/wiki/Folded-prerequisites">Folded Prerequisites</a></div>
<h2>Comments</h2>
<div class='comments'>
<div class='comment'>
<div class='author'>Hema Powar</div>
<div class='content'>
Hey, saw your write ups. It is quite interesting. Please share your profile at hema.powar@wipro.com if you are looking for job. We have projects for a reputed client in Cupertino CA. Think you might have guessed the client already :-) <br /><br />Reach out to me at 201-272-6303</div>
</div>
</div>
